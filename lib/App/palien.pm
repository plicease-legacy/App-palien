package App::palien;

use strict;
use warnings;
use Getopt::Long qw( GetOptions );
use Pod::Usage qw( pod2usage );
use Path::Class qw( file dir );

# ABSTRACT: Command line interface to Alien::Base
# VERSION

=head1 DESCRIPTION

This module provides the machinery for the command line
program L<palien>.

=head1 SEE ALSO

L<palien>

=cut

sub main
{
  local(undef, @ARGV) = @_;
  
  my $cflags;
  my $libs;
  my $status = 0;
  
  GetOptions(
    "cflags"     =>   \$cflags,
    "libs"       =>   \$libs,
    "help|h"     =>   sub { pod2usage({ -verbose => 2}) },
    "version"    =>   sub { print "App::palien version " . ($App::palien::VERSION || 'dev') . "\n"; exit 1; },
  ) || pod2usage(1);
  
  foreach my $module (@ARGV)
  {
    my $alien;
  
    if($module =~ /::/)
    {
      $alien = eval qq{ require $module; $module->new };
      if($@)
      {
        warn "unable to load $module: $@";
        $status = 2;
        next;
      }
    }
    else
    {
      $alien = find($module);
      next unless $alien;
    }
    
    if($cflags)
    {
      print $alien->cflags, "\n";
    }
    
    if($libs)
    {
      print $alien->libs, "\n";
    }
  }
  
  $status;
}

my $byname;

sub find
{
  my($q) = @_;

  unless($byname)
  {
    $byname->{libarchive} = 'Alien::Libarchive';
    foreach my $inc (@INC)
    {
      my $share_root = dir( $inc, qw( auto share dist ) );
      next unless -d $share_root;
      foreach my $share ($share_root->children)
      {
        my $readme = $share->file('README');
        next unless (-r $readme) && ($readme->slurp =~ /This README file is autogenerated by Alien::Base/);
        my $module = $share->basename;
        $module =~ s/-/::/g;
        my $name = eval qq{ require $module\::ConfigData; $module\::ConfigData->config('name'); };
        $byname->{$name} = $module;
      }
    }
  }
  
  my $module = $byname->{$q};
  
  defined $module ? eval qq{ require $module; $module->new } || warn "unable to load $module: $@" && () : ();
}

1;
